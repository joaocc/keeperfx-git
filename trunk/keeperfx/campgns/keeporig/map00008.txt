REM LEVEL 8

REM NEW THINGS FOR THE PLAYER :
REM TEMPLE - OK
REM POWER_PROTECT - OK
REM POWER_CONCEAL - OK
REM STEEL - OK
REM HORNED_REAPER - NEXT LEVEL, BUT CAN BE POSSIBLE TO SUMMON HERE IF THE PLAYER IS TESTING THE SACRIFICES - CORRECTED

SET_GENERATE_SPEED(550)

MAX_CREATURES(PLAYER0,30)
MAX_CREATURES(PLAYER1,27)

START_MONEY(PLAYER0,9000)
START_MONEY(PLAYER1,9000)

COMPUTER_PLAYER(PLAYER1,2)

SET_CREATURE_MAX_LEVEL(PLAYER0,DEMONSPAWN,10)

REM	ALL PLAYERS

REM	CREATURE AVAILABILITY

		CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)

		ADD_CREATURE_TO_POOL(FLY,10)
		ADD_CREATURE_TO_POOL(DEMONSPAWN,20)
		ADD_CREATURE_TO_POOL(TROLL,20)
		ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)
		ADD_CREATURE_TO_POOL(SORCEROR,30)
		ADD_CREATURE_TO_POOL(BILE_DEMON,10)
		ADD_CREATURE_TO_POOL(ORC,10)
			
REM	ROOM AVAILABILITY

		ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)

REM	SPELLS AVAILABILITY
		
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)

REM	DOORS AND TRAPS AVAILABILITY

		DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
		DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
		TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
		TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
		DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)

REM -----------------------------------------------------------------

REM "TREAD CAREFULLY DUNGEON KEEPER.  RIVERS OF FIRE SNAKE THROUGH THIS REALM.  FIND THE ENEMY KEEPER AND DESPATCH HIM TO HELL."
DISPLAY_OBJECTIVE(76,PLAYER0)

IF_AVAILABLE(PLAYER0,POWER_CONCEAL == 1)
REM	"YOU HAVE RESEARCHED THE POWER OF INVISIBILITY.  CAST IT ON A CREATURE TO HIDE THEM FROM ENEMY EYES."
	DISPLAY_INFORMATION(77)
ENDIF

IF_AVAILABLE(PLAYER0,POWER_PROTECT == 1)
REM	"THE POWER TO PROTECT YOUR CREATURES HAS BEEN RESEARCHED.  CAST IT ON A CREATURE TO STRENGTHEN THEIR RESILIENCE."
	DISPLAY_INFORMATION(78)
ENDIF

IF(PLAYER1,DUNGEON_DESTROYED == 1)
	IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
		DISPLAY_OBJECTIVE(19,PLAYER0)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,KNIGHT,-1,1,5,3000)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,-1,2,3,3000)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-1,2,3,3000)
		SET_FLAG(PLAYER0,FLAG0,1)
	ENDIF
ENDIF
	 		
IF(PLAYER0,FLAG0 == 1)
	IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
REM		"THE ENEMY IS NO MORE.  THE INHABITANTS OF THIS REGION WILL BOW DOWN BEFORE YOUR EVIL."
		DISPLAY_OBJECTIVE(79,PLAYER0)
		WIN_GAME
	ENDIF
ENDIF

IF_AVAILABLE(PLAYER0,TEMPLE == 1)
REM	"A TEMPLE HAS BEEN RESEARCHED.  SACRIFICE CREATURES INTO THE POOL FOR GIFTS FROM THE GODS."
	DISPLAY_INFORMATION(80)
ENDIF

IF_AVAILABLE(PLAYER0,STEEL == 1)
REM	"YOU HAVE MANUFACTURED A STEEL DOOR.  THIS IS THE MOST POWERFUL PHYSICAL DOOR."
	DISPLAY_INFORMATION(87)
ENDIF

IF(PLAYER0,HORNY >= 1)
	DISPLAY_INFORMATION(83)
ENDIF